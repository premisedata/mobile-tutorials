import com.premise.mobile.tutorials.extensions.ProjectPropertyExtensionsKt

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.premise.mobile.tutorials"
        minSdk 21
        targetSdk 32
        versionCode 1

        // Use the Project.getVersionName() extension function (defined in :buildSrc)
        // to set the app version name based on the property located within `gradle.properties`
        versionName ProjectPropertyExtensionsKt.getVersionName(project)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    // Use the Project.shouldLogAnalytics extension property (defined in :buildSrc)
    // to add a BuildConfig property that can be used within project code
    //
    // When using from Groovy, we must use extension property through a getter on the Java class
    // generated from the Kotlin source file that contains the extension
    buildTypes {
        debug {
            buildConfigField("boolean", "SHOULD_LOG_ANALYTICS", ProjectPropertyExtensionsKt.getShouldLogAnalytics(project).toString())
        }
        release {
            minifyEnabled false
            buildConfigField("boolean", "SHOULD_LOG_ANALYTICS", ProjectPropertyExtensionsKt.getShouldLogAnalytics(project).toString())
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
}